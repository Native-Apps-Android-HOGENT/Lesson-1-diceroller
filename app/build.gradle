apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

ext {
    minimumSdkVersion = 19
    targetSdkVersion = 28
    versionMajor = 1
    versionMinor = 0
    versionPatch = 1
    versionClassifier = null
    isSnapshot = true
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "be.equality.diceroller"
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode generateVersionCode()
        versionName generateVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier == null && ext.isSnapshot) {
        ext.versionClassifier = "SNAPSHOT"
    }

    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$version_kotlin"

    // Android
    implementation "androidx.appcompat:appcompat:$version_appcompat"

    // Layout
    implementation "androidx.constraintlayout:constraintlayout:$version_constraint_layout"

    // Testing
    testImplementation "junit:junit:$version_junit"
    androidTestImplementation "androidx.test.espresso:espresso-core:$version_espresso"
    androidTestImplementation "androidx.test:runner:$version_testrunner"
    androidTestImplementation "androidx.test:rules:$version_testrules"
    androidTestImplementation "androidx.test.ext:junit:$version_androidx_junit"

    //Simple logging functionality
    implementation "com.jakewharton.timber:timber:$version_timber"
}
